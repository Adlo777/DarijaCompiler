# =======================================
# Sample Survivor -- Darija Compiler
# =======================================

# Initialise le monde
init(15, 15, rgb(255, 0, 0))

# Crée 50 véhicules placés aléatoirement
men i = 0, i < 50, i++ {
    ila rand() < 0.5 {
        truck(rand(0, getNbX() - 1), rand(0, getNbY() - 1))
    } makantch {
        car(rand(0, getNbX() - 1), rand(0, getNbY() - 1))
    }
}

# Ajoute de la vie aux véhicules
trucks [
    current.life = 2
]
cars [
    current.life = 1
]

# Fait avancer les véhicules
survivor = s7i7
mahed survivor {
    survivor = khat2a
    # Pour chaque véhicule ...
    all [
        # S'il a encore de la vive, part au combat
        ila current.life > 0 {
            # Recherche des ennemis à proximité
            front = current.pickForward()
            back = current.pickBackward()
            # S'il y en a devant ou derrière, attaque
            ila front.count() > 0 {
                current.forward()
            } makantch ila back.count() > 0 {
                current.backward()
            # Sinon, bouge
            } makantch {
                action = rand(0, 4)
                ila action <= 1 {
                    current.backward()
                } makantch ila action >= 3 {
                    current.forward()
                } makantch {
                    current.turn()
                }
            }
            # Gestion des bords
            ila current.getX() >= getNbX() {
                current.setX(current.getX() - getNbX())
            }
            ila current.getX() < 0 {
                current.setX(getNbX() - (current.getX() + 2))
            }
            ila current.getY() >= getNbY() {
                current.setY(current.getY() - getNbY())
            }
            ila current.getY() < 0 {
                current.setY(getNbY() - (current.getY() + 2))
            }
            # Inflige des dommages lors d'une collision
            ila current.isTruck() {
                damage = 2
            } makantch {
                damage = 1
            }
            over = current.pickOver()
            over [
                current.life -= damage
                ila current.life <= 0 {
                    remove(current)
                }
            ]
            survivor = s7i7
            paint(0.1)
        }
    ]
}

# Peut durer très très très longtemps ... ^^

# Affichage final
setColor(rgb(255, 255, 255))
paint(10)