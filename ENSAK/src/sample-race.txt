# =======================================
# World Race -- Darija Compiler
# =======================================

# Initialise le monde
init(30, 10, rgb(20, 20, 20))

# Crée 10 véhicules
men i = 0, i < 10, i++ {
    ila rand() < 0.5 {
 # Aléatoirement des camions ou des voiture
        truck(0, i)
    } makantch {
        car(0, i)
    }
}

# Ajoute de l'essence aux véhicules
trucks [
    current.essence = 40
]
cars [
    current.essence = 20
]

# Ajoute un compteur de distance à tous les véhicules
all [
    current.distance = 0
]

# Fait avancer les véhicules
founded = s7i7
mahed founded {
    founded = khat2a
    # Pour chaque véhicule ...
    all [
        # S'il a encore de l'essence, avance
        ila current.essence > 0 {
            # Avance de 0, 1 ou 2 cases en avant
            nb = rand(0, 2)
            men i = 0, i <= nb, i++ {
                current.forward()
                current.distance++
            }
            # S'il est au bout du terrain, recommence au début
            ila current.getX() >= getNbX() {
                current.setX(current.getX() - getNbX())
            }
            # Gestion de la consommation d'essence
            ila current.isTruck() {
                current.essence -= 2
            } makantch {
                current.essence -= 1
            }
            founded = s7i7
            paint(0.1)
        }
    ]
}

# Recherche le vainqueur
setColor(rgb(255, 255, 255))
winner = 0
all [
    ila winner == 0 {
        winner = current
    } makantch {
        ila winner.distance < current.distance {
            winner = current
        }
    }
]

# Supprime un à un les perdants
all [
    ila current != winner {
        remove(current)
        paint(1)
    }
]

# Fait faire la fête au vainqueur
setColor(rgb(0, 255, 0))
men i = 0, i <= 15, i++ {
    winner.turn()
    paint(0.1)
}

# Affichage final
paint(5)